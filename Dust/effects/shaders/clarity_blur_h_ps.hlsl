// Horizontal Gaussian blur for Clarity local contrast extraction.
// Large-radius separable blur to isolate low-frequency content.

Texture2D sceneTex       : register(t0);
SamplerState linearClamp : register(s0);

cbuffer ClarityParams : register(b0)
{
    float2 viewportSize;
    float2 invViewportSize;
    float  strength;
    float  midtoneProtect;
    float  blurRadius;
    float  _pad;
    // Normalized gaussian weights for |offset| = index, precomputed on the
    // CPU (sigma = radius / 3). Saves up to 65 exp() per pixel per pass.
    float4 blurWeights[33];
};

float4 main(float4 pos : SV_Position, float2 uv : TEXCOORD0) : SV_Target
{
    float3 total = float3(0, 0, 0);

    int iRadius = (int)ceil(blurRadius);
    iRadius = min(iRadius, 32);

    [loop]
    for (int i = -iRadius; i <= iRadius; i++)
    {
        float w = blurWeights[abs(i)].x;
        float2 sampleUV = uv + float2(float(i) * invViewportSize.x, 0.0);
        total += sceneTex.SampleLevel(linearClamp, sampleUV, 0).rgb * w;
    }

    return float4(total, 1.0);
}
